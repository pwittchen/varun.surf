plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.github.pwittchen'
version = project.hasProperty('version') && project.version != 'unspecified' ? project.version : getVersionFromGitTag()
description = 'varun.surf'

static def getVersionFromGitTag() {
    try {
        def gitTag = 'git describe --tags --abbrev=0'.execute().text.trim()
        return gitTag ?: '0.0.1-SNAPSHOT'
    } catch (Exception e) {
        print(e)
        return 'local'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    jvmArgs '--enable-preview'
}

tasks.withType(JavaExec).configureEach {
    jvmArgs '--enable-preview'
}

processResources {
    filesMatching('application.yml') {
        expand(project.properties)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', "1.0.2")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'org.javatuples:javatuples:1.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'com.google.truth:truth:1.4.4'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

jacoco {
    toolVersion = "0.8.13"
}

tasks.named('test') {
    finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport') {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }
}

tasks.register('buildFrontend', Exec) {
    commandLine 'bash', '-c', 'npm install && npm run build'
}

tasks.register('cleanFrontend', Exec) {
    commandLine 'bash', '-c', 'npm run clean'
}

tasks.named('build') {
    dependsOn 'buildFrontend'
}

tasks.named('bootRun') {
    dependsOn 'buildFrontend'
}