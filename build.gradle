plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.github.pwittchen'
version = project.hasProperty('version') && project.version != 'unspecified' ? project.version : getVersionFromGitTag()
description = 'varun.surf'

static def getVersionFromGitTag() {
    try {
        def gitTag = 'git describe --tags --abbrev=0'.execute().text.trim()
        return gitTag ?: '0.0.1-SNAPSHOT'
    } catch (Exception e) {
        print(e)
        return 'local'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

processResources {
    filesMatching('application.yml') {
        expand(project.properties)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', "1.0.2")
}

sourceSets {
    e2e {
        java {
            srcDir 'src/e2e/java'
        }
        resources {
            srcDir 'src/e2e/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    e2eImplementation.extendsFrom testImplementation
    e2eRuntimeOnly.extendsFrom testRuntimeOnly
}

jacoco {
    toolVersion = "0.8.13"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.javatuples:javatuples:1.2'
    implementation 'com.google.guava:guava:33.5.0-jre'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'com.google.truth:truth:1.4.5'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'com.microsoft.playwright:playwright:1.49.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    jvmArgs '--enable-preview'
}

tasks.withType(JavaExec).configureEach {
    jvmArgs '--enable-preview'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('test') {
    finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport') {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }
}

tasks.register('buildFrontend', Exec) {
    commandLine 'sh', '-c', 'bun install && bun run build'
}

tasks.register('cleanFrontend', Exec) {
    commandLine 'sh', '-c', 'bun run clean'
}

tasks.named('build') {
    dependsOn 'buildFrontend'
}

tasks.named('bootRun') {
    dependsOn 'buildFrontend'
}

tasks.register('run') {
    dependsOn 'bootRun'
}

tasks.named('bootJar') {
    dependsOn 'buildFrontend'
}

tasks.register('testE2e', Test) {
    description = 'Runs E2E tests with Playwright in headless mode'
    group = 'verification'
    testClassesDirs = sourceSets.e2e.output.classesDirs
    classpath = sourceSets.e2e.runtimeClasspath
    useJUnitPlatform()
    jvmArgs '--enable-preview'
    systemProperty 'playwright.headless', 'true'
    dependsOn 'assemble'
    outputs.upToDateWhen { false }
}

tasks.register('testE2eNoHeadless', Test) {
    description = 'Runs E2E tests with Playwright in visible browser mode'
    group = 'verification'
    testClassesDirs = sourceSets.e2e.output.classesDirs
    classpath = sourceSets.e2e.runtimeClasspath
    useJUnitPlatform()
    jvmArgs '--enable-preview'
    systemProperty 'playwright.headless', 'false'
    dependsOn 'assemble'
    outputs.upToDateWhen { false }
}